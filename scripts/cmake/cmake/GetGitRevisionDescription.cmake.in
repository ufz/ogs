#
# Internal file for GetGitRevisionDescription.cmake
#
# Requires CMake 2.6 or newer (uses the 'function' command)
#
# Original Author:
# 2009-2010 Ryan Pavlik <rpavlik@iastate.edu> <abiryan@ryand.net>
# http://academic.cleardefinition.com
# Iowa State University HCI Graduate Program/VRAC
#
# Copyright Iowa State University 2009-2010.
# Distributed under the Boost Software License, Version 1.0.
# (See accompanying file LICENSE_1_0.txt or copy at
# http://www.boost.org/LICENSE_1_0.txt)

set(HEAD_HASH)
set(GIT_D)

file(READ "@HEAD_FILE@" HEAD_CONTENTS LIMIT 1024)

string(STRIP "${HEAD_CONTENTS}" HEAD_CONTENTS)
if(HEAD_CONTENTS MATCHES "ref")
    # named branch
    string(REPLACE "ref: " "" HEAD_REF "${HEAD_CONTENTS}")

    set(GIT_D "@GIT_DIR@")
    # to make secondary git working directory work
    if(EXISTS "${GIT_D}/commondir")
        file(READ "${GIT_D}/commondir" cdir_contents LIMIT 1024)
        string(STRIP "${cdir_contents}" cdir_contents)
        set(GIT_D "${GIT_D}/${cdir_contents}")
    endif()

    if(EXISTS "${GIT_D}/${HEAD_REF}")
        configure_file("${GIT_D}/${HEAD_REF}" "@GIT_DATA@/head-ref" COPYONLY)
    elseif(EXISTS "${GIT_D}/logs/${HEAD_REF}")
        configure_file("${GIT_D}/logs/${HEAD_REF}" "@GIT_DATA@/head-ref" COPYONLY)
        set(HEAD_HASH "${HEAD_REF}")
    endif()
else()
    # detached HEAD
    configure_file("@GIT_DIR@/HEAD" "@GIT_DATA@/head-ref" COPYONLY)
endif()

if(NOT HEAD_HASH)
    file(READ "@GIT_DATA@/head-ref" HEAD_HASH LIMIT 1024)
    string(STRIP "${HEAD_HASH}" HEAD_HASH)
endif()
